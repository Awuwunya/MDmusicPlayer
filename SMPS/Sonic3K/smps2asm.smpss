### SONIC3K SMPS2ASM SCRIPT MADE BY NATSUMI 17/09/10
sversion v1_1
sdatamacro dc.b

c ('\type\' != 'voice') && ('\type\' != 'sfx') && ('\type\' != '') && ('\type\' != 'music') {
	+Available types: Music, SFX, Voice
	$.cc
	;
}

=endian 'little'
=upb 0x17D8

c '\type\' == 'voice' {
	=offset 0
	~£_Patches .of
	/ >patch
	;
}

@offset 0 "Please specify offset of this song in Z80 bank"

c '\type\' == 'sfx' {
	/ >headersfx
} {
	/ >header
}

?header {
	! > sHeaderInit: {
		~£_Header .of
		%; Z80 offset is {$4!\offset\}
	}
	c .nw == \upb\ {
		! > sHeaderPatchUniv:;
		$ .sw
	} {
		! > sHeaderPatch: .dw {
			~£_Patches :0
		}
	}

	=FMnum .db
	=PSGnum .nb
	! > sHeaderCh: .lb, .db;
	! > sHeaderTempo: .db, .db;
	! > sHeaderDAC: .dw {
		~£_DAC :0
	}
	$ .sw
	f \FMnum\ - 1 {
		! > sHeaderFM: .dw, .db, .db {
			~£_FM? :0
		}
	}

	f \PSGnum\ {
		! > sHeaderPSG: .dw, .db, .db, .db, .db {
			~£_PSG? :0
			:?4 {
				?volenv;
			}
		}
	}

	/£_FM? ?note ?comm
	/£_PSG? ?note ?comm
	/£_DAC ?DAC ?comm
	/£_Patches >patch
}

?headersfx {
	! > sHeaderInit: {
		~£_Header .of
		%; Z80 offset is {$4!\offset\}
	}

	c .nw == \upb\ {
		! > sHeaderPatchUniv:;
		$ .sw
	} {
		! > sHeaderPatch: .dw {
			~£_Patches :0
		}
	}

	! > sHeaderTick: .db;
	=CHnum .db
	! > sHeaderCh: \CHnum\;
	=trpd 0
	=trpn 0

	f \CHnum\ {
		>f 1
		=trpd .nb
		>b 1
		c (\trpd\ & 0x80) == 0 {
			=trpn (\trpd\&3)+1

			c (\trpd\ & 4) != 0 {
				=trpn \trpn\+3
			}

			! > sHeaderSFX: .db, .db, .dw, .db, .db {
				~£_FM\trpn\ :2
			}
		} {
			=trpn ((\trpd\&0x60)>>5)+1
			! > sHeaderSFX: .db, .db, .dw, .db, .db {
				~£_PSG\trpn\ :2
			}
		}

	}

	/£_FM? ?note ?comm
	/£_PSG? ?note ?comm
	/£_Patches >patch
}

?patch {
	=num 0
	=b1 0
	=b2 0
	=b3 0
	=b4 0
	f (.an-.pc)/25 {
		%\n\t; Patch {$2!\num\}\r\n\t; {$2!.db}\r\n\t; {$2!.db}, {$2!.db}, {$2!.db}, {$2!.db},\t{$2!.db}, {$2!.db}, {$2!.db}, {$2!.db}\r\n\t; {$2!.db}, {$2!.db}, {$2!.db}, {$2!.db},\t{$2!.db}, {$2!.db}, {$2!.db}, {$2!.db}\r\n\t; {$2!.db}, {$2!.db}, {$2!.db}, {$2!.db},\t{$2!.db}, {$2!.db}, {$2!.db}, {$2!.db}
		>b 25
		! > spAlgorithm: .nb&0x7;
		! > spFeedback: (.db&0x38)>>3;
		=b1 .db
		=b3 .db
		=b2 .db
		=b4 .db
		! > spDetune: (\b1\&0xF0)>>4, (\b2\&0xF0)>>4, (\b3\&0xF0)>>4, (\b4\&0xF0)>>4;
		! > spMultiple: \b1\&0xF, \b2\&0xF, \b3\&0xF, \b4\&0xF;
		=b1 .db
		=b3 .db
		=b2 .db
		=b4 .db
		! > spRateScale: (\b1\&0xC0)>>6, (\b2\&0xC0)>>6, (\b3\&0xC0)>>6, (\b4\&0xC0)>>6;
		! > spAttackRt: \b1\&0x1F, \b2\&0x1F, \b3\&0x1F, \b4\&0x1F;
		=b1 .db
		=b3 .db
		=b2 .db
		=b4 .db
		! > spAmpMod: (\b1\&0x80)>>7, (\b2\&0x80)>>7, (\b3\&0x80)>>7, (\b4\&0x80)>>7;
		! > spSustainRt: \b1\&0x1F, \b2\&0x1F, \b3\&0x1F, \b4\&0x1F;
		=d1 .db
		=d3 .db
		=d2 .db
		=d4 .db
		=b1 .db
		=b3 .db
		=b2 .db
		=b4 .db
		! > spSustainLv: (\b1\&0xF0)>>4, (\b2\&0xF0)>>4, (\b3\&0xF0)>>4, (\b4\&0xF0)>>4;
		! > spDecayRt: \d1\, \d2\, \d3\, \d4\;
		! > spReleaseRt: \b1\&0xF, \b2\&0xF, \b3\&0xF, \b4\&0xF;
		=b1 .db
		=b3 .db
		=b2 .db
		! > spTotalLv: \b1\&0x7F, \b2\&0x7F, \b3\&0x7F, .db&0x7F;
		=num \num\+1
	}
}

?volenv {
	=v00 0
	=v01 1
	=v02 2
	=v03 3
	=v04 4
	=v05 5
	=v06 6
	=v07 7
	=v08 8
	=v09 9
	=v0A 0x0A
	=v0B 0x0B
	=v0C 0x0C
	=v0D 0x0D
	=v0E 0x0E
	=v0F 0x0F
	=v10 0x10
	=v11 0x11
	=v12 0x12
	=v13 0x13
	=v14 0x14
	=v15 0x15
	=v16 0x16
	=v17 0x17
	=v18 0x18
	=v19 0x19
	=v1A 0x1A
	=v1B 0x1B
	=v1C 0x1C
	=v1D 0x1D
	=v1E 0x1E
	=v1F 0x1F
	=v20 0x20
	=v21 0x21
	=v22 0x22
	=v23 0x23
	=v24 0x24
	=v25 0x25
	=v26 0x26
	=v27 0x27
}

?modenv {
	=m00 0
	=m01 1
	=m02 2
	=m03 3
	=m04 4
	=m05 5
	=m06 6
	=m07 7
	=m08 8
}

?DAC {
	=nRst 0x80
	=dSnare 0x81
	=dHighTom 0x82
	=dMidTom 0x83
	=dLowTom 0x84
	=dFloorTom 0x85
	=dKick 0x86
	=dMuffledSnare 0x87
	=dCrashCymbal 0x88
	=dRideCymbal 0x89
	=dLowMetalHit 0x8A
	=dMetalHit 0x8B
	=dHighMetalHit 0x8C
	=dHigherMetalHit 0x8D
	=dMidMetalHit 0x8E
	=dClap 0x8F
	=dElectricHighTom 0x90
	=dElectricMidTom 0x91
	=dElectricLowTom 0x92
	=dElectricFloorTom 0x93
	=dTightSnare 0x94
	=dMidpitchSnare 0x95
	=dLooseSnare 0x96
	=dLooserSnare 0x97
	=dHiTimpani 0x98
	=dLowTimpani 0x99
	=dMidTimpani 0x9A
	=dQuickLooseSnare 0x9B
	=dClick 0x9C
	=dPowerKick 0x9D
	=dQuickGlassCrash 0x9E
	=dGlassCrashSnare 0x9F
	=dGlassCrash 0xA0
	=dGlassCrashKick 0xA1
	=dQuietGlassCrash 0xA2
	=dOddSnareKick 0xA3
	=dKickExtraBass 0xA4
	=dComeOn 0xA5
	=dDanceSnare 0xA6
	=dLooseKick 0xA7
	=dModLooseKick 0xA8
	=dWoo 0xA9
	=dGo 0xAA
	=dSnareGo 0xAB
	=dPowerTom 0xAC
	=dHiWoodBlock 0xAD
	=dLowWoodBlock 0xAE
	=dHiHitDrum 0xAF
	=dLowHitDrum 0xB0
	=dMetalCrashHit 0xB1
	=dEchoedClapHit 0xB2
	=dLowerEchoedClapHit 0xB3
	=dHipHopHitKick 0xB4
	=dHipHopHitPowerKick 0xB5
	=dBassHey 0xB6
	=dDanceStyleKick 0xB7
	=dHipHopHitKick2 0xB8
	=dHipHopHitKick3 0xB9
	=dReverseFadingWind 0xBA
	=dScratch 0xBB
	=dLooseSnareNoise 0xBC
	=dPowerKick2 0xBD
	=dCrashingNoiseWoo 0xBE
	=dQuickHit 0xBF
	=dKickHey 0xC0
	=dPowerKickHit 0xC1
	=dLowPowerKickHit 0xC2
	=dLowerPowerKickHit 0xC3
	=dLowestPowerKickHit 0xC4
	=dEchoedClapHit2 0xC5
	=dLowerEchoedClapHit2 0xC6
}

	sinc "..\SMPS\SMPS default notes.smpss"

?comm {
	!0xE0 > sPan: .db & 0xC0, .lb & 0x3F {
		:?0 {
			=spNone 0x00
			=spRight 0x40
			=spLeft 0x80
			=spCenter 0xC0
		}

		c .n1 == 0 {
			:-1
		}
	}
	!0xE1 > ssDetune: .db;
	!0xE2 > sFade: .db;
	!0xE3 > sStopFM: {
		;
	}
	!0xE4 > ssVol: $2!-(.db | 0x80);
	!0xE5 > saVolFMP: .db, .db;
	!0xE6 > saVolFM: .db;
	=sHold 0xE7
	!0xE8 > sNoteTimeOut: .db;
	!0xE9 > sSpDashRev:;
	!0xEA > sPlayDAC: .db | 0x80;
	!0xEB > sLoopExit: .db, .dw {
		~£_Jumpe? :1
	}
	!0xEC > saVolPSG: .db;
	!0xED > ssTransposeS3K: .db;
	!0xEE > sYM1cmd: .db, .db;
	!0xEF, 0-0x7F > sPatFM: .lb & 0x7F;
	!0xEF, 0x80-0xFF > sPatFM: .lb & 0x7F, .db - 0x81;
	!0xF0 > ssModZ80: .db, .db, .db, .db;
	!0xF1 > sModEnv: .db, .db {
		:?0 {
			?modenv;
		}
		:?1 {
			?modenv;
		}
	}
	!0xF2 > sStop:{
		;
	}
	!0xF3 > sNoisePSG: .db;
	!0xF4, 0x80 > sModOn:;
	!0xF4 > sModEnv: .lb {
		:?0 {
			?modenv;
		}
	}
	!0xF5 > sVolEnvPSG: .db {
		:?0 {
			?volenv;
		}
	}
	!0xF6 > sJump: .dw {
		~£_Jump? :0
		;
	}
	!0xF7 > sLoop: .db, .db, .dw {
		~£_Loop? :2
	}
	!0xF8 > sCall: .dw {
		~£_Call? :0
	}
	!0xF9 > sRet: {
		;
	}
	!0xFA > sModOff:;
	!0xFB > saTranspose: .db;
	!0xFC > sCont: .dw {
		~£_Continous? :0
	}
	!0xFD > sRawFreq: .db;
	!0xFE > sSpecFM3: .db, .db, .db, .db {
		%; Broken?
	}
	!0xFF, 0 > ssTempo: .db;
	!0xFF, 1 > sPlaySound: .db;
	!0xFF, 2 > sMusPause: .db;
	!0xFF, 3 > sCopyMem: .dw, .db {
		%; No song should ever use this command. It may good idea to remove it.
	}
	!0xFF, 4 > ssTickMul: .db;
	!0xFF, 5 > sSSGEG: .db, .db, .db, .db {
		=op2 .a2
		:=2 .a1
		:=1 \op2\
	}
	!0xFF, 6 > sVolEnvFM: .db, .db;
	!0xFF, 7 > sSpDashReset:;
}
